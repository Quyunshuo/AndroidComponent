apply plugin: 'com.android.application'

def version = rootProject.ext.versions
def depend = rootProject.ext.dependencies

android {
    compileSdkVersion version.compileSdkVersion
    buildToolsVersion version.buildToolsVersion

    defaultConfig {
        applicationId version.applicationId
        minSdkVersion version.minSdkVersion
        targetSdkVersion version.targetSdkVersion
        versionCode version.versionCode
        versionName version.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        //ARouter路由配置
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
    }

    signingConfigs {
        releaseConfig {
            storeFile file('UG41E0bF.jks')
            storePassword "UG41E0bF"
            keyAlias "UG41E0bF"
            keyPassword "UG41E0bF"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseConfig
            applicationVariants.all { variant ->
                variant.outputs.all { output ->
                    if (variant.name.endsWith("release")) {
                        def debugFileName = "Android_Component_${variant.versionCode}_${variant.versionName}_${releaseTime()}_release.apk"
                        outputFileName = new File(debugFileName)
                    }
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
}

static def releaseTime() {
    return new Date().format("yyyyMMdd-hh-mm", TimeZone.getTimeZone("GMT+08:00"))
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])


    //公用依赖包
    implementation project(':common_base')
    if (!Boolean.valueOf(rootProject.ext.isModule)) {
        //main模块
        implementation project(':main_module')
        implementation project(':me_module')
        implementation project(':other_module')
    }

    //阿里路由
    annotationProcessor depend["arouter_compiler"]
}
